amends "package://github.com/jdx/hk/releases/download/v1.2.0/hk@1.2.0#/Config.pkl"
import "package://github.com/jdx/hk/releases/download/v1.2.0/hk@1.2.0#/Builtins.pkl"

// Fast checks for pre-commit (format, lint, types only)
local fastChecks = new Mapping<String, Step> {
    ["ruff:format"] = (Builtins.ruff) {
        dir = "backend"
        glob = List("*.py")
        stage = List("*")
        check = "mise run check:format"
        fix = "mise run fix:format"
    }
    ["ruff:lint"] = (Builtins.ruff) {
        dir = "backend"
        glob = List("*.py")
        stage = List("*")
        check = "mise run check:lint"
        fix = "mise run fix:lint"
    }
    ["ty"] = (Builtins.mypy) {
        dir = "backend"
        glob = List("src/**/*.py")
        check = "mise run check:types"
    }
}

// Comprehensive checks including tests (for pre-push and manual runs)
local comprehensiveChecks = new Mapping<String, Step> {
    ...fastChecks
    ["test:backend"] {
        dir = "backend"
        glob = List("*.py")
        check = "mise run test-ci"
    }
    ["build:frontend"] {
        dir = "frontend"
        glob = List("src/**/*.{ts,tsx}", "package.json", "vite.config.ts", "tsconfig.json")
        check = "make build"
    }
    ["test:frontend"] {
        dir = "frontend"
        glob = List("src/**/*.{ts,tsx}", "tests/**/*.{ts,feature}")
        check = "make test"
    }
}

hooks {
    ["pre-commit"] {
        fix = true              // runs the fix step to make modifications
        stash = "patch-file"    // stashes unstaged changes before running fix steps
        steps = fastChecks      // Only fast checks on pre-commit (< 5 seconds)
    }
    ["pre-push"] {
        steps = comprehensiveChecks  // Full test suite on pre-push
    }

    // "fix" and "check" are special steps for `hk fix` and `hk check` commands
    ["fix"] {
        fix = true
        steps = comprehensiveChecks
    }
    ["check"] {
        steps = comprehensiveChecks
    }
}
